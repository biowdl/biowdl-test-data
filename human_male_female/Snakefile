
rule download_hg38_chromosome:
    params:
        # Download the GRCh38 genome hard-masked from ensembl.
        url="ftp://ftp.ensembl.org/pub/release-98/fasta/homo_sapiens/dna/Homo_sapiens.GRCh38.{chrom}.fa.gz"
    output:
        "GRCh38/Homo_sapiens.GRCh38.{chrom}.fa.gz"
    singularity:
        # Contains curl based on debian-slim
        "docker://biowdl/curl@sha256:3b0477d934547d8c274c9834372e791342d2a97409eedde40e786c7782721f53"
    shell:
        "curl -s -o {output} {params.url}"

rule concatenate_ref_genome:
    input:
        "GRCh38/Homo_sapiens.GRCh38.dna.chromosome.22.fa.gz",
        "GRCh38/Homo_sapiens.GRCh38.dna.chromosome.X.fa.gz",
        "GRCh38/Homo_sapiens.GRCh38.dna.chromosome.Y.fa.gz",
    output:
        "temp/22XY.fa"
    singularity:
        # Debian:buster-slim pinned at a hash.
        "docker://debian@sha256:a467ec784be2fa9d9e1868c4bf0c5c5792750453db3e5cd1d726a7243536a54f"
    shell:
        "gzip -cd {input} > {output}"

rule create_small_reference:
    input:
        hg38="temp/22XY.fa",
        bed="extract.bed"
    output:
         "reference.fa"
    singularity:
        # Bedtools 2.29 does weird things to the name of the contig. 2.27 acts normally.
        "docker://quay.io/biocontainers/bedtools:2.27.1--he860b03_3"
    shell:
         # Use -name to get the name from the bed file.
         "bedtools getfasta -name -fi {input.hg38} -bed {input.bed} "
         "> {output}"

# rule create_sample_data:
#     input:
#          hg38="reference_unmasked.fa",
#          vcf="mutations.vcf",
#          ploidy_file="{sample}.ploidy"
#     output:
#         "{sample}/{sample}.fa"
